###############################################################################
# This module belongs to Steve Kieffer's *MathNotes* Library.
# It is licensed under the Creative Commons Attribution-ShareAlike 4.0
# International License (CC BY-SA 4.0):
#   https://creativecommons.org/licenses/by-sa/4.0/
# Attribution may be made through hyperlink to the following URL:
#   https://github.com/skieffer/mathnotes
###############################################################################

from gh.rrmath.lit.H.ilbert.ZB.Thm118 import Pf1
from gh.rrmath.lit.H.ilbert.ZB.Pg180L1 import Rem as RemarkOnP180

deduc X1 of Pf1.R6 {

    rels R10 {

        asrt A1 {
            sy = "$@n(\delta) = n(-1) n(\ell) n(\zeta)^{\ell-1} / n(1-\zeta)$"
        }

        asrt A2 {
            sy = "
            $@= (-1)^{\ell-1} \cdot \ell^{\ell-1}
            \cdot 1 / \ell$
            "
        }

        asrt A3 {
            sy = "$@ = \ell^{\ell-2}$"
        }
    }

    asrt A15 {
        en = "$@\ell - 1$ is even"
    }

    rels R20 {

        asrt A1 {
            sy = "$@n(\zeta) = \zeta^{1+2+\cdots+(\ell-1)}$"
        }

        asrt A2 {
            sy = "$@ = \zeta^{\ell(\ell-1)/2}$"
        }

        asrt A3 {
            sy = "$@ = 1^{(\ell-1)/2}$"
        }
    }

    asrt A30 {
        sy = "$@F(x) = x^{\ell-1} + x^{\ell-2} + \cdots + x + 1$"
    }

    asrt A40 {
        sy = "$@F(x) = (x - \zeta)(x - \zeta^2) \cdots (x - \zeta^{\ell-1})$"
    }

    rels R50 {

        asrt A1 {
            sy = "
            $@n(1 - \zeta) =
            (1 - \zeta)(1 - \zeta^2) \cdots (1 - \zeta^{\ell-1})$
            "
        }

        asrt A2 {
            sy = "$@ = F(1)$"
        }

        asrt A3 {
            sy = "$@ = \ell$"
        }
    }

    meson = "
    From Pf1.A4 get R10.A1.
    Have R10.A2 by R20 and R50.
    But R10.A3 by A15.
    Have R50.A2 by A40.
    From A30 get R50.A3.
    But Pf1.R6 by RemarkOnP180 and Pf1.I5 and R10.
    "
}
