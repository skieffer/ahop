# --------------------------------------------------------------------------- #
# AHOP: an arithmetical house of pancakes                                     #
# Copyright (c) 2018-2022 Steve Kieffer                                       #
#                                                                             #
# This Source Code Form is subject to the terms of the Mozilla Public         #
# License, v. 2.0. If a copy of the MPL was not distributed with this         #
# file, You can obtain one at http://mozilla.org/MPL/2.0/.                    #
# --------------------------------------------------------------------------- #

from gh.alpinemath.histarch.G.alois import M1

# Modernized versions of the proofs in Galois' first memoir

deduc Lem3 {
    
    intr I1 {
        en="
        Let an equation $h(y) = 0$ with distinct roots
        $@a, b, c, d, ddd$ be given.
        "
    }
    
    intr I2 {
        en="
        Let $@V = vph(a, b, c, d, ddd)$ be as in Lemma 2.
        "
    }
    
    asrt C {
        en="
        Each of the roots $@a, b, c, d, ddd$ can be expressed
        as a rational function of $V$.
        "
    }
    
    meson="Let I1. Let I2. Then C."
}

deduc Pf3 of Lem3.C {
    
    intr I10 {
        en="
        Form the function
          $@f(x) = bqnt x - vph(a, b, c, d, ddd); bqnt x - vph(a, c, b, d, ddd);
           bqnt x - vph(a, b, d, c, ddd); ccc$
        where the product ranges over all those permutations of the roots that hold $a$ fixed.
        "
    }
    
    asrt A20 {
        en="
        The coefficients of $f(x)$ are symmetrical in $@b, c, d, ddd$.
        "
    }
    
    asrt A30 {
        en="
        The coefficients of $f(x)$ can be written as rational functions in $a$.
        "
    }
    
    exis E40 {
        en="There are % such that"
        intr I1 {
            en="
            rational functions $@c1(y), c2(y), ddd$
            "
        }
        intr I2 {
            en="
            and a bivariate function
                $@F(x, y) = x^m + c1(y) xuum-1 + c2(y) xuum-2 + ccc$
            "
        }
        asrt A {
            sy="$@F(x, a) = f(x)$"
        }
    }
    
    asrt A45 {
        sy="$@F(V, a) = 0$"
    }
    
    intr I50 {
        en="
        Use the Euclidean Algorithm to compute
        the GCD $g(y)$ of $F(V, y)$ and $h(y)$.
        "
    }
    
    asrt A60 {
        sy="$@g(y) = y - a$"
    }
    
    subdeduc D70 {
        
        supp S10 {
            en="
            Suppose $@F(V, y)$ and $h(y)$ had any other
            common solution, say $b$, besides $a$.
            "
        }
        
        asrt A20 {
            sy="$@F(V, b) = 0$"
        }
        
        asrt A30 {
            en="
            The functions $F(x, a)$ and $F(x, b)$ have a common
            root at $V$, so their GCD has positive degree.
            "
        }
        
        # ...?
        
        asrt A1000 {
            en="
            One of the functions $@vph(a, ddd)$ must equal
            one of the functions $@vph(b, ddd)$.
            "
        }
        
        flse F2000 contra S10 {}
        
        meson="
        Suppose S10. Then A20, so A30.
        !
        A1000, hence F2000, by Lem3.I2.
        "
    }
    
    asrt A80 {
        en="
        The coefficients of $g(y)$ are
        rational functions in $V$.
        "
    }
    
    asrt A90 {
        en="
        $a$ is expressible as a
        rational function in $V$.
        "
    }
    
    meson="
    Let I10. Then A20, so A30. Therefore E40, so A45.
    Now let I50. Then A60, by A45 and D70.
    Therefore A90 by A80 by I50, hence Lem3.C.
    "
}

deduc x1 of Pf3.A30 {
    
    intr I10 {
        en="
        Consider $h(y)$ as a polynomial over $@bbQ(a)$,
        and compute the quotient $@q(y) in bbQ(a)$ satisfying
                 $h(y) = (y - a)q(y)$
        using the division algorithm.
        "
    }
    
    asrt A20 {
        en="
        The roots of $q(y)$ are precisely
        $@b, c, d, ddd$.
        "
    }
    
    asrt A30 {
        en="
        The coefficients of $q(y)$ are the elementary
        symmetric functions in $@b, c, d, ddd$.
        "
    }
    
    asrt A40 {
        en="
        The coefficients of $q(y)$ are rational
        functions in $a$.
        "
    }
    
    meson="
    Let I10. Then A20, so A30. But A40 by I10.
    Therefore Pf3.A30, using Pf3.A20 and A30.
    "
}
